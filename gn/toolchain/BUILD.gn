#  Toolchains
#
#  Copyright 2021 MobSlicer152
#
#  Licensed under the Apache License, Version 2.0 (the "License");
#  you may not use this file except in compliance with the License.
#  You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
#  Unless required by applicable law or agreed to in writing, software
#  distributed under the License is distributed on an "AS IS" BASIS,
#  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#  See the License for the specific language governing permissions and
#  limitations under the License.

toolchain("clang") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "clang -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }
  tool("asm") {
    # For Clang we can just use the C compiler to compile assembly.
    depfile = "{{output}}.d"
    command = "clang -MMD -MF $depfile {{defines}} {{include_dirs}} {{asmflags}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "ASM {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }
  tool("alink") {
    command = "rm -f {{output}} && ar rcs {{output}} {{inputs}}"
    description = "AR {{target_output_name}}{{output_extension}}"
    outputs =
        [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = ".a"
    output_prefix = "lib"
  }
  tool("solink") {
    soname = "{{target_output_name}}{{output_extension}}"  # e.g. "libfoo.so".
    sofile = "{{output_dir}}/$soname"
    rspfile = soname + ".rsp"
    if (is_mac) {
      os_specific_option = "-install_name @executable_path/$sofile"
      rspfile_content = "{{inputs}} {{solibs}} {{libs}}"
    } else {
      os_specific_option = "-Wl,-soname=$soname"
      rspfile_content = "-Wl,--whole-archive {{inputs}} {{solibs}} -Wl,--no-whole-archive {{libs}}"
    }
    command =
        "clang -shared {{ldflags}} -o $sofile $os_specific_option @$rspfile"
    description = "SOLINK $soname"

    # Use this for {{output_extension}} expansions unless a target manually
    # overrides it (in which case {{output_extension}} will be what the target
    # specifies).
    default_output_extension = ".so"

    # Use this for {{output_dir}} expansions unless a target manually overrides
    # it (in which case {{output_dir}} will be what the target specifies).
    default_output_dir = "{{root_out_dir}}"
    outputs = [ sofile ]
    link_output = sofile
    depend_output = sofile
    output_prefix = "lib"
  }
  tool("link") {
    outfile = "{{target_output_name}}{{output_extension}}"
    rspfile = "$outfile.rsp"
    if (is_mac) {
      command = "clang {{ldflags}} -o $outfile @$rspfile {{solibs}} {{libs}}"
    } else {
      command = "clang {{ldflags}} -o $outfile -Wl,--start-group @$rspfile {{solibs}} -Wl,--end-group {{libs}}"
    }
    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    rspfile_content = "{{inputs}}"
    outputs = [ outfile ]
  }
  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }
  tool("copy") {
    command = "cp -af {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}

if (current_cpu == "x64") {
  asm = "ml64"
} else {
  asm = "ml"
}

toolchain("msvc") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "cl /nologo /sourceDependencies $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} /c /Tc {{source}} /FS /Fo{{output}}"
    depsformat = "msvc"
    description = "CC {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.obj" ]
  }
  tool("rc") {
    command = "rc /nologo /r /fo{{output}} {{source}}"
    description = "RC {{output}}"
    outputs = [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.res" ]
  }
  tool("asm") {
    # For MSVC use ml(64)
    depfile = "{{output}}.d"
    command = "cmd /c \"$asm /nologo {{defines}} {{include_dirs}} {{asmflags}} /Fo{{output}} /c /Ta{{source}} | findstr /v /i assembling & echo > nul\""
    depsformat = "msvc"
    description = "ASM {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.obj" ]
  }
  tool("alink") {
    command = "lib /nologo /out:{{output}} {{inputs}}"
    description = "AR {{target_output_name}}{{output_extension}}"
    outputs =
        [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = ".lib"
    output_prefix = "lib"
  }
  tool("solink") {
    dllname = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
    libname = "${dllname}.lib"  # e.g. foo.dll.lib
    pdbname = "${dllname}.pdb"
    rspfile = "${dllname}.rsp"
    command = "cmd /c \"link /nologo /dll /implib:$libname /out:$dllname @$rspfile | findstr /v /i Creating & echo > nul\""
    description = "LINK(DLL) $dllname"

    # Use this for {{output_extension}} expansions unless a target manually
    # overrides it (in which case {{output_extension}} will be what the target
    # specifies).
    default_output_extension = ".dll"

    # Use this for {{output_dir}} expansions unless a target manually overrides
    # it (in which case {{output_dir}} will be what the target specifies).
    default_output_dir = "{{root_out_dir}}"

    outputs = [
      dllname,
      libname,
      pdbname,
    ]

    link_output = libname
    depend_output = libname
    runtime_outputs = [
      dllname,
      libname,
      pdbname,
    ]
    output_prefix = "lib"
    rspfile_content =
        "{{libs}} {{solibs}} {{inputs_newline}} {{ldflags}} {{rlibs}}"
  }
  tool("link") {
    exename = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
    pdbname = "${exename}.pdb"
    rspfile = "$exename.rsp"
    command = "cmd /c \"link /out:$exename /nologo @$rspfile | findstr /v /i Creating & echo > nul\""

    default_output_extension = ".exe"
    default_output_dir = "{{root_out_dir}}"
    description = "LINK {{output}}"
    outputs = [
      exename,
      pdbname,
    ]
    runtime_outputs = outputs

    # The use of inputs_newline is to work around a fixed per-line buffer
    # size in the linker.
    rspfile_content =
        "{{inputs_newline}} {{libs}} {{solibs}} {{ldflags}} {{rlibs}}"
  }
  tool("stamp") {
    command = "cmd /c type nul >> {{output}}"
    description = "STAMP {{output}}"
  }
  tool("copy") {
    command = "xcopy /q/y {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}

toolchain("stuff") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "tcc -MD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }
  tool("asm") {
    # For Clang we can just use the C compiler to compile assembly.
    depfile = "{{output}}.d"
    command = "tcc -MD -MF $depfile {{defines}} {{include_dirs}} {{asmflags}} -xa -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "ASM {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }
  tool("alink") {
    command = "del /f/q {{output}} && tcc -ar rcs {{output}} {{inputs}}"
    description = "AR {{target_output_name}}{{output_extension}}"
    outputs =
        [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = ".lib"
    output_prefix = ""
  }
  tool("solink") {
    soname = "{{target_output_name}}{{output_extension}}"  # e.g. "libfoo.so".
    sofile = "{{output_dir}}/$soname"
    rspfile = soname + ".rsp"
    if (is_mac) {
      os_specific_option = "-install_name @executable_path/$sofile"
      rspfile_content = "{{inputs}} {{solibs}} {{libs}}"
    } else {
      os_specific_option = "-Wl,-soname=$soname"
      rspfile_content = "-Wl,--whole-archive {{inputs}} {{solibs}} -Wl,--no-whole-archive {{libs}}"
    }
    command = "tcc -shared {{ldflags}} -o $sofile $os_specific_option @$rspfile"
    description = "SOLINK $soname"

    # Use this for {{output_extension}} expansions unless a target manually
    # overrides it (in which case {{output_extension}} will be what the target
    # specifies).
    default_output_extension = ".dll"

    # Use this for {{output_dir}} expansions unless a target manually overrides
    # it (in which case {{output_dir}} will be what the target specifies).
    default_output_dir = "{{root_out_dir}}"
    outputs = [ sofile ]
    link_output = sofile
    depend_output = sofile
    output_prefix = ""
  }
  tool("link") {
    outfile = "{{target_output_name}}{{output_extension}}"
    rspfile = "$outfile.rsp"
    if (is_mac) {
      command = "tcc {{ldflags}} -o $outfile @$rspfile {{solibs}} {{libs}}"
    } else {
      command = "tcc {{ldflags}} -o $outfile -Wl,--start-group @$rspfile {{solibs}} -Wl,--end-group {{libs}}"
    }
    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    rspfile_content = "{{inputs}}"
    outputs = [ outfile ]
  }
  tool("stamp") {
    command = "cmd /c type NUL >> {{output}}"
    description = "STAMP {{output}}"
  }
  tool("copy") {
    command = "xcopy /q/y {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}
